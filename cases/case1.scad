/* FrameOS Case 1 */

/* [View settings] */

view_mode="print_vertical"; // [print_vertical, print_horizontal, stacked, exploded, only_case, only_thick_border, only_panel_cover]

// Shrinkage adjustment when printing vertically. By default 0.4mm of shrinkage for every 100mm of print height.
vertical_print_scale = 1.004;

/* [Case depth] */

panel_cover_depth = 1.6; // Panel cover thickness
panel_depth  = 1.2; // Thickness of the eInk panel as measured
case_depth = 10.0; // Total inside compartment thickness
back_depth = 1.2; // Back plate thickness 
fillet_radius = 2.5; // How round to make the case. Not used with thick borders.

/* [Panel dimensions] */

panel_width  = 284.49; // Physical width of the eInk panel (mm)
panel_height = 208.81; // Physical height of the eInk panel (mm)

clearance = 0.3; // Clearance around the eInk panel (mm)

// Dimensions of the bezel on the eInk panel. This part will be covered by the panel cover.
panel_bezel_left   = 11.7;
panel_bezel_right  = 3.3;
panel_bezel_top    = 3.1;
panel_bezel_bottom = 3.1;

/* [Border and inner padding] */

// How much border to add around the panel
panel_border_left   = 3.2;
panel_border_right  = 11.6;
panel_border_top    = 11.8;
panel_border_bottom = 11.8;

// Supporting space below the panel
case_inner_padding_left = 4;
case_inner_padding_right = 4;
case_inner_padding_top = 4;
case_inner_padding_bottom = 4;

/* [Cover/case thickness reversal] */

// Move border case depth from the case body to the cover.
reverse_border_thickness = false;
reverse_border_gap = 0.3;
reverse_border_left_width = panel_border_left;
reverse_border_right_width = panel_border_right;
reverse_border_top_width = panel_border_top;
reverse_border_bottom_width = panel_border_bottom;
reverse_border_panel_lead_in_depth = max(fillet_radius - back_depth, 0);

/* [Thick border] */
thick_border = false;
thick_border_width = 12.0;
thick_border_extra_depth = 15.0;

/* [Colors] */
case_color = "yellow";
panel_cover_color = "white";
thick_border_color = "gray";
kickstand_color = "white";

/* [Cable holes for panel] */

// Gap in the border for the eInk panel's cables, mm, centered
panel_cable_gap_left = 0;
panel_cable_gap_right = 0;
panel_cable_gap_top = 0;
panel_cable_gap_bottom = 0;
panel_cable_gap_size = 2.0; // How into the border to cut
case_cable_gap_depth = 3.0; // How deep into the case to cut

/* [Panel supports] */

case_center_support_vertical = true;
case_center_support_horizontal = true;
case_center_support_width = 4.0;

/* [Screws and placement] */

// Center of each screw hole from the corner
screw_offset_left   = 5.0;
screw_offset_right  = 5.0;
screw_offset_top    = 5.0;
screw_offset_bottom = 5.0;

extra_screws_top = [0.48, 0.52, 0, 0, 0]; // Extra screws on the top (0.0 to 1.0)
extra_screws_bottom = [0.48, 0.52, 0, 0, 0]; // Extra screws on the bottom (0.0 to 1.0)
extra_screws_left = [0, 0, 0, 0, 0]; // Extra screws on the left (0.0 to 1.0)
extra_screws_right = [0.5, 0, 0, 0, 0]; // Extra screws on the right (0.0 to 1.0)

// Hole made into panel cover
panel_screw_insert_diameter = 3.45;
panel_screw_insert_depth = 2.0;

// Cylindrical hole inserted into case
case_screw_hole_diameter        = 4.5;
case_screw_hole_thread_diameter = 2.0; // Hole for the screw thread that goes all the way through
case_screw_hole_floor_depth     = 1.0; // Depth of the floor of the screw hole
case_screw_hole_insert_depth    = 4.0; // Leave this much room at bottom for the heat set insert

reverse_border_screw_post_diameter = max(case_screw_hole_diameter + 1.5, panel_screw_insert_diameter + 2.0);

/* [Case side holes] */

case_hole_left_top = 0;
case_hole_left_bottom = 0;
case_hole_right_top = 0;
case_hole_right_bottom = 0;
case_hole_top_left = 0;
case_hole_top_right = 0;
case_hole_bottom_left = 0;
case_hole_bottom_right = 0;

case_hole_left_top_offset = 0;
case_hole_left_bottom_offset = 0;
case_hole_right_top_offset = 0;
case_hole_right_bottom_offset = 0;
case_hole_top_left_offset = 0;
case_hole_top_right_offset = 0;
case_hole_bottom_left_offset = 0;
case_hole_bottom_right_offset = 0;

case_hole_top_depth = 1;
case_hole_bottom_depth = 1;

/* [Kickstand] */

kickstand = true;
kickstand_width = 95;
kickstand_leg_width = 10;
kickstand_height_percentage = 65;
kickstand_leg_bridge_offset_percentage = 15;
kickstand_leg_bridge_height = 7;
kickstand_depth = 7;
kickstand_bottom_start = 2;
kickstand_leg_bottom_padding = 0.2;
kickstand_leg_bottom_angle = 80; // [45:90] Angle of the sloped bottom face of the kickstand feet
kickstand_wall_thickness = 1;
kickstand_gap_thickness = 0.5;
kickstand_hinge_diameter = 2.2;
kickstand_hinge_top_extra_leverage = 3; // Height added to the flap above the hinge. Increasing reduces max rotation
kickstand_hinge_wall_padding = 0.2; // Distance from the back wall
kickstand_hinge_cylinder_gap = 0.5; // Gap between the hinge and the cylinder
kickstand_rotation = 0; // Kickstand rotation angle, goes up to 35 when open
kickstand_inner_snap = true;
kickstand_inner_snap_reverse = true;
kickstand_inner_snap_height_percentage = 50;
kickstand_inner_snap_radius_hole = 1.0;
kickstand_inner_snap_radius_ball = 1.5;
kickstand_inner_snap_gap = 1.2;

/* [USB cutout] */

usb_cutout = true;
usb_cutout_offset_x_percentage = 80;
usb_cutout_offset_y_percentage = 20;
usb_cutout_box_width = 20;
usb_cutout_box_height = 50;
usb_cutout_box_depth = 7;
usb_cutout_left_wall_thickness = 1.4;
usb_cutout_right_wall_thickness = 1.4;
usb_cutout_bottom_wall_thickness = 1.4;
usb_cutout_back_wall_thickness = 0.8;
usb_cutout_hole_offset = 0;
usb_cutout_hole_position = "top"; // [top, bottom, left, right, back]
// Larger USB-C
// usb_cutout_hole_width = 14;
// usb_cutout_hole_height = 6.0;
// usb_cutout_top_wall_thickness = 1.4;
// Tiny USB-C
usb_cutout_hole_width = 9.1;
usb_cutout_hole_height = 3.2;
usb_cutout_top_wall_thickness = 5.0;

/* [SD cutout] */

sd_cutout = false;
sd_cutout_offset_x_percentage = 80;
sd_cutout_offset_y_percentage = 20;
sd_cutout_box_width = 20;
sd_cutout_box_height = 50;
sd_cutout_box_depth = 7;
sd_cutout_left_wall_thickness = 1.4;
sd_cutout_right_wall_thickness = 1.4;
sd_cutout_bottom_wall_thickness = 1.4;
sd_cutout_back_wall_thickness = 0.8;
sd_cutout_hole_offset = 0;
sd_cutout_hole_position = "top"; // [top, bottom, left, right, back]
sd_cutout_hole_width = 9.1;
sd_cutout_hole_height = 3.2;
sd_cutout_top_wall_thickness = 5.0;

/* [Hole to cut out of the back] */
hole_cutout = true;
hole_cutout_offset_x_percentage = 62;
hole_cutout_offset_y_percentage = 76.4;
hole_cutout_box_width = 20;
hole_cutout_box_height = 13;
hole_cutout_box_depth = 7;


/* [Hanging hole] */
// [ [top, bottom, left, right], offset_percentage ]
hanging_holes = [
    // ["top", 18],
    // ["left", 18],
    // ["right", 18],
    // ["bottom", 18]
];
hanging_hole_large_diameter = 8;
hanging_hole_small_diameter = 3;
hanging_hole_box_width = 12;
hanging_hole_box_height = 16;
hanging_hole_depth = 7;
hanging_hole_wall_thickness = 1;

/* [SD card adapter cutout] */

sd_card_in_leg = false;
sd_card_in_leg_shape = "adapter"; // [adapter, hole]
sd_card_in_leg_side = "left"; // [left, right]
sd_card_in_leg_side_side = "left"; // [left, right]
sd_card_in_leg_y_percentage = 75.5;
sd_card_in_leg_hole_offset = 1.8; // for shape=hole... mm from base

sd_card_in_usb_cutout = false;
sd_card_in_usb_cutout_side = "right"; // [left, right]
sd_card_in_usb_cutout_y_percentage = 75.5;

/* [Rear cooling] */
rear_cooling = false;
rear_cooling_x_start_percentage = 6.1;
rear_cooling_x_end_percentage = 20;
rear_cooling_y_start_percentage = 20;
rear_cooling_y_end_percentage = 60;
rear_cooling_radius = 2;
rear_cooling_gap = 5;
rear_cooling_offset = 5;

/* [Raspberry Pi Zero Pinholes] */
pi_pinholes = false;
pi_pinholes_cooling = true;
pi_pinholes_cooling_gap = 5.7;
pi_pinholes_orientation = "vertical"; // [horizontal, vertical]
pi_pinholes_x_percentage = 5.5;
pi_pinholes_y_percentage = 15;
pi_pinholes_diameter = 2.5;
pi_pinholes_spacer = 4.1;
pi_pinholes_spacer_height = 3;
pi_pinholes_height = 23;
pi_pinholes_width = 58;

/* [Extra Pin Mounts] */
extra_pin_mounts_enabled = false;
extra_pin_mounts_anchor = "center"; // [center, percentage, top-left, top, top-right, left, right, bottom-left, bottom, bottom-right]
extra_pin_mounts_anchor_percentage = [50, 15]; // Used when anchor is "percentage".
extra_pin_mounts_hole_diameter = 0; // Blind hole through the raised spacer; 0 disables it.
// Each mount is [x_offset, y_offset, pin_diameter, spacer_diameter, spacer_height, pin_enabled].
// Use [x_offset, y_offset] to keep the Raspberry Pi pin defaults, or undef for any optional value.
extra_pin_mounts = [
    [0, 0]
];

/* [Hidden] */
extra_pinholes = false;
extra_pinholes_anchor = "center"; // [center, percentage, top-left, top, top-right, left, right, bottom-left, bottom, bottom-right]
extra_pinholes_anchor_percentage = [50, 15]; // Used when anchor is "percentage".
extra_pinholes_offsets = [
    [0, 0]
];

/* [Backplate Access Holes] */
backplate_access_holes = false;
backplate_access_holes_diameter = 2;
backplate_access_holes_anchor = "center"; // [center, percentage, top-left, top, top-right, left, right, bottom-left, bottom, bottom-right]
backplate_access_holes_anchor_percentage = [50, 15]; // Used when anchor is "percentage".
backplate_access_holes_offsets = [
    [0, 0]
];


/* [Side buttons] */
side_buttons_left = [];
side_buttons_right = [];
side_buttons_top = [];
side_buttons_bottom = [];
side_button_width = 9.2;
side_button_height = 3.2;
side_button_extrude = 1.5; // How much it comes out of the case
side_button_base = 1; // Height of square rectangle below the button
side_button_base_border = 1; // Extra around width and height
side_button_base_inner = 1.0; // How much does the button base go inside the case body
side_button_hole_gap = 0.3; // How much more to carve out of the hole
side_button_fillet_radius = 0.0; // Radius of the fillet on the button

/* [Debug] */
// Gap between STL parts for visual debugging
debug_gap = 40;
// Gap on baseplate between case and body when printing
print_gap = 10 + (thick_border ? thick_border_width : 0);
cross_section_percentage = 0; // [0:100]

// End of variables.

$fn = 32;

/*****************************************************************************/
/*                Derived Dimensions (overall frame size)                    */
/*****************************************************************************/

// Physical width of the eInk panel (mm) + clearance 0.3*2
panel_width_with_clearance  = panel_width  + clearance * 2;
// Physical height of the eInk panel (mm) + clearance 0.3*2
panel_height_with_clearance = panel_height + clearance * 2;

// Width and height of the entire frame
frame_full_width  = panel_width_with_clearance
                  + panel_border_left + panel_border_right;

frame_full_height = panel_height_with_clearance
                  + panel_border_top + panel_border_bottom;

// Total depth across all 4 layers
frame_full_depth = panel_depth
                 + panel_cover_depth
                 + case_depth
                 + back_depth;

fillet_radius_in_use = thick_border ? 0 : fillet_radius;

kickstand_height = frame_full_height * kickstand_height_percentage / 100;
kickstand_full_width = kickstand_width + 2 * kickstand_wall_thickness + 2 * kickstand_gap_thickness;
kickstand_leg_full_width = kickstand_leg_width + 2 * kickstand_wall_thickness + 2 * kickstand_gap_thickness;
kickstand_leg_bridge_offset = kickstand_leg_bridge_offset_percentage * (kickstand_height - 2 * kickstand_leg_bridge_height) / 100;

usb_cutout_x = (frame_full_width - usb_cutout_box_width - usb_cutout_left_wall_thickness - usb_cutout_right_wall_thickness) * usb_cutout_offset_x_percentage / 100;
usb_cutout_y = (frame_full_height - usb_cutout_box_height - usb_cutout_top_wall_thickness - usb_cutout_bottom_wall_thickness) * usb_cutout_offset_y_percentage / 100;

usb_sd_card_x_position = sd_card_in_usb_cutout_side == "left" ? usb_cutout_x + usb_cutout_left_wall_thickness + 0.11 : usb_cutout_box_width + usb_cutout_left_wall_thickness + usb_cutout_x - 0.11;
usb_sd_card_y_position = usb_cutout_y + 12 + sd_card_in_usb_cutout_y_percentage / 100 * (usb_cutout_box_height -24);

sd_cutout_x = (frame_full_width - sd_cutout_box_width - sd_cutout_left_wall_thickness - sd_cutout_right_wall_thickness) * sd_cutout_offset_x_percentage / 100;
sd_cutout_y = (frame_full_height - sd_cutout_box_height - sd_cutout_top_wall_thickness - sd_cutout_bottom_wall_thickness) * sd_cutout_offset_y_percentage / 100;

hole_cutout_x = (frame_full_width - hole_cutout_box_width) * hole_cutout_offset_x_percentage / 100;
hole_cutout_y = (frame_full_height - hole_cutout_box_height) * hole_cutout_offset_y_percentage / 100;

/*****************************************************************************/
/*                 Utility: Corner Screw Hole Positions                      */
/*****************************************************************************/

// Coordinates for the four corners, offset in from each edge
screw_positions = [
    [ screw_offset_left,                     screw_offset_bottom                  ],
    [ frame_full_width - screw_offset_right, screw_offset_bottom                  ],
    [ screw_offset_left,                     frame_full_height - screw_offset_top ],
    [ frame_full_width - screw_offset_right, frame_full_height - screw_offset_top ],

    for (s = extra_screws_bottom)
        if (s != 0 && s != 1)
            [ screw_offset_left + s * (frame_full_width - screw_offset_left - screw_offset_right), 
            frame_full_height - screw_offset_top ],

    for (s = extra_screws_top)
        if (s != 0 && s != 1)
            [ screw_offset_left + s * (frame_full_width - screw_offset_left - screw_offset_right), 
            screw_offset_bottom ],

    for (s = extra_screws_left)
        if (s != 0 && s != 1)
            [ screw_offset_left, 
            screw_offset_bottom + s * (frame_full_height - screw_offset_bottom - screw_offset_top) ],

    for (s = extra_screws_right)
        if (s != 0 && s != 1)
            [ frame_full_width - screw_offset_right, 
            screw_offset_bottom + s * (frame_full_height - screw_offset_bottom - screw_offset_top) ]
];

function hanging_hole_position(edge, offset_percentage) = 
    let (
        offset = offset_percentage * (edge == "left" || edge == "right" 
            ? frame_full_width - hanging_hole_box_height 
            : frame_full_height - hanging_hole_box_height) / 100
    ) [
        edge == "left" 
            ? offset
            : edge == "right"
                ? frame_full_width - offset - hanging_hole_box_height
                : (frame_full_width - hanging_hole_box_width) / 2,
        edge == "top"
            ? offset
            : edge == "bottom"
                ? frame_full_height - offset - hanging_hole_box_height
                : (frame_full_height - hanging_hole_box_width) / 2
    ];

/*****************************************************************************/
/*                            Panel cover                                    */
/*****************************************************************************/

module panel_cover() {
    difference() {
        union() {
            // Top block
            filletBoxTop(frame_full_width, frame_full_height, panel_cover_depth + panel_depth);

            reverse_border_panel_cover_extension();
        }
        
        // Window for the eInk panel’s visible area
        translate(
          [
            panel_bezel_left + panel_border_left, 
            panel_bezel_top  + panel_border_top,
            -0.11
          ]
        )
        cube([
            panel_width_with_clearance  - panel_bezel_left - panel_bezel_right, 
            panel_height_with_clearance - panel_bezel_top  - panel_bezel_bottom, 
            panel_cover_depth + 0.22
        ]);

        for (c = screw_positions) {
            translate([c[0], c[1],  panel_cover_depth + panel_depth - panel_screw_insert_depth])
                cylinder(d = panel_screw_insert_diameter, 
                         h = panel_screw_insert_depth + (reverse_border_thickness ? case_depth : 0) + 0.11);
        }

        // Internal rectangular cutout matching the panel
        translate(
          [
            panel_border_left,
            panel_border_top,
            panel_cover_depth
          ]
        )
        cube([
            panel_width_with_clearance,
            panel_height_with_clearance,
            panel_depth + 0.22
        ]);
        if (panel_cable_gap_bottom > 0) {
            render_panel_cable_gap_bottom(panel_cover_cable_gap_depth(), panel_cover_depth);
        }
        if (panel_cable_gap_top > 0) {
            render_panel_cable_gap_top(panel_cover_cable_gap_depth(), panel_cover_depth);
        }
        if (panel_cable_gap_left > 0) {
            render_panel_cable_gap_left(panel_cover_cable_gap_depth(), panel_cover_depth);
        }
        if (panel_cable_gap_right > 0) {
            render_panel_cable_gap_right(panel_cover_cable_gap_depth(), panel_cover_depth);
        }

        reverse_border_panel_cover_side_button_holes();
    }
}

function panel_cover_cable_gap_depth() =
    panel_depth + (reverse_border_thickness ? case_depth : 0);

module reverse_border_panel_cover_extension() {
    if (reverse_border_thickness && case_depth > 0) {
        difference() {
            union() {
                reverse_border_panel_cover_strips();
                reverse_border_panel_cover_screw_posts();
            }

            reverse_border_kickstand_keepouts();
        }
    }
}

module reverse_border_panel_cover_strips() {
    difference() {
        translate([0, 0, panel_cover_depth + panel_depth - 0.01])
        reverse_border_panel_cover_outer_shape(case_depth + 0.01);

        translate([
            reverse_border_left_width,
            reverse_border_top_width,
            panel_cover_depth + panel_depth - 0.02
        ])
        cube([
            frame_full_width - reverse_border_left_width - reverse_border_right_width,
            frame_full_height - reverse_border_top_width - reverse_border_bottom_width,
            case_depth + 0.04
        ]);
    }
}

module reverse_border_panel_cover_outer_shape(depth) {
    lead_in_depth = min(reverse_border_panel_lead_in_depth, depth);
    if (lead_in_depth > 0) {
        intersection() {
            filletBoxBottom(
                frame_full_width,
                frame_full_height,
                depth + lead_in_depth,
                r=fillet_radius_in_use
            );

            cube([frame_full_width, frame_full_height, depth]);
        }
    } else {
        filletBoxMiddle(frame_full_width, frame_full_height, depth);
    }
}

module reverse_border_panel_cover_screw_posts() {
    intersection() {
        union() {
            for (c = screw_positions) {
                translate([
                    c[0],
                    c[1],
                    panel_cover_depth + panel_depth - 0.01
                ])
                cylinder(d = reverse_border_screw_post_diameter, h = case_depth + 0.01);
            }
        }

        reverse_border_panel_cover_strips();
    }
}

module reverse_border_kickstand_keepouts() {
    if (kickstand) {
        let (
            leg_x_starts_full = [
                (frame_full_width - kickstand_full_width) / 2,
                (frame_full_width + kickstand_full_width) / 2 - kickstand_leg_full_width,
            ],
            leg_y = frame_full_height - kickstand_bottom_start - kickstand_height,
            leg_bridge_leg_y = frame_full_height - kickstand_bottom_start - kickstand_leg_bridge_offset - kickstand_leg_bridge_height * 2,
            leg_bridge_full_y = leg_bridge_leg_y - kickstand_wall_thickness,
            bridge_x = leg_x_starts_full[0] + kickstand_leg_full_width - kickstand_wall_thickness * 2,
            bridge_y = leg_bridge_full_y + kickstand_depth - kickstand_wall_thickness,
            bridge_width = kickstand_full_width - kickstand_leg_full_width * 2 + kickstand_wall_thickness * 4,
            bridge_height = kickstand_leg_bridge_height * 2 + kickstand_wall_thickness * 2 - kickstand_depth * 2
        ) {
            for (x = leg_x_starts_full) {
                reverse_border_keepout_cube(
                    x - reverse_border_gap,
                    leg_y - reverse_border_gap,
                    kickstand_leg_full_width + reverse_border_gap * 2,
                    kickstand_height + reverse_border_gap * 2
                );
            }

            reverse_border_keepout_cube(
                bridge_x - reverse_border_gap,
                bridge_y - reverse_border_gap,
                bridge_width + reverse_border_gap * 2,
                bridge_height + reverse_border_gap * 2
            );
        }
    }
}

module reverse_border_keepout_cube(x, y, width, height) {
    if (width > 0 && height > 0) {
        translate([x, y, panel_cover_depth + panel_depth - 0.02])
        cube([width, height, case_depth + 0.04]);
    }
}

module reverse_border_panel_cover_side_button_holes() {
    if (reverse_border_thickness) {
        translate([0, 0, panel_cover_depth + panel_depth])
        sideButtonHoles();
    }
}

module render_panel_cable_gap_bottom(depth, translate_depth) {
    cubeWithAngledTopBottom(
        loc=[
            panel_border_left + panel_width_with_clearance / 2 - panel_cable_gap_bottom / 2,
            panel_border_top + panel_height_with_clearance - case_inner_padding_bottom - case_depth - 0.11,
            translate_depth
        ],
        size=[
            panel_cable_gap_bottom,
            panel_cable_gap_size + case_inner_padding_bottom + 0.11 + case_depth,
            depth + 0.11
        ]
    );
}

module render_panel_cable_gap_top(depth, translate_depth) {
    cubeWithAngledTopBottom(
        loc=[
            panel_border_left + panel_width_with_clearance / 2 - panel_cable_gap_top / 2,
            panel_border_top - panel_cable_gap_size,
            translate_depth
        ],
        size=[
            panel_cable_gap_top,
            panel_cable_gap_size * 2 + case_inner_padding_top + 0.11, // 2x to cut into the chamfer
            depth + 0.11
        ],
        top=(view_mode == "print_vertical"),
        topReverse=true
    );
}

module render_panel_cable_gap_left(depth, translate_depth) {
    cubeWithAngledTopBottom(
        loc=[
            panel_border_left - panel_cable_gap_size,
            panel_border_top + panel_height_with_clearance / 2 - panel_cable_gap_left / 2,
            translate_depth
        ],
        size=[
            panel_cable_gap_size + case_inner_padding_left + 0.11,
            panel_cable_gap_left,
            depth + 0.11
        ],
        top=(view_mode == "print_vertical"),
        topReverse=true
    );
}

module render_panel_cable_gap_right(depth, translate_depth) {
    cubeWithAngledTopBottom(
        loc=[
            panel_border_left + panel_width_with_clearance - case_inner_padding_right - 0.11,
            panel_border_top + panel_height_with_clearance / 2 - panel_cable_gap_right / 2,
            translate_depth
        ],
        size=[
            panel_cable_gap_size + case_inner_padding_right + 0.11,
            panel_cable_gap_right,
            depth + 0.11
        ],
        top=(view_mode == "print_vertical"),
        topReverse=true
    );
}

/*****************************************************************************/
/*                             Case body                                     */
/*****************************************************************************/

module caseBody () {
    difference() {
        filletBoxBottom(
            frame_full_width, 
            frame_full_height, 
            case_depth + back_depth
        );
        
        // Internal rectangular cutout for electronics
        translate(
          [
            panel_border_left + case_inner_padding_left,
            panel_border_top  + case_inner_padding_top,
            -0.11
          ]
        )
        cube([
            panel_width_with_clearance 
              - case_inner_padding_left 
              - case_inner_padding_right,
            panel_height_with_clearance 
              - case_inner_padding_top 
              - case_inner_padding_bottom,
            case_depth + 0.11
        ]);
        case_cable_gaps();
        case_holes();
    }
     // chamfer the edges
    if (view_mode == "print_vertical") {
        difference() {
            // top chamfer
            let (
                l = panel_width_with_clearance - case_inner_padding_left - case_inner_padding_right, 
                w = -case_depth, 
                h = -case_depth
            )
            translate([
                panel_border_left + case_inner_padding_left,
                panel_border_top  + case_inner_padding_top + case_depth,
                case_depth
            ])
            polyhedron(//pt 0        1        2        3        4        5
                points=[[0,0,0], [l,0,0], [l,w,0], [0,w,0], [0,w,h], [l,w,h]],
                faces=[[0,1,2,3],[5,4,3,2],[0,4,5,1],[0,3,4],[5,2,1]]
            );
            if (panel_cable_gap_top > 0) {
                render_panel_cable_gap_top(case_cable_gap_depth, -0.11);
            }
        }
    }
}

module caseThickBorder () {
    difference() {
        color(thick_border_color)
        translate([- thick_border_width, -thick_border_width, -thick_border_extra_depth + 0.01])
        cube([
            frame_full_width + 2 * thick_border_width, 
            frame_full_height + 2 * thick_border_width, 
            case_depth + back_depth + thick_border_extra_depth
        ]);

        color(thick_border_color)
        translate([0, 0, -thick_border_extra_depth - 11])
        cube([
            frame_full_width,
            frame_full_height,
            case_depth + back_depth + thick_border_extra_depth + 22
        ]);

        sideButtonHoles();
    }
}

module case_cable_gaps() {
    if (panel_cable_gap_bottom > 0) {
        render_panel_cable_gap_bottom(case_cable_gap_depth, -0.11);
    }
    if (panel_cable_gap_top > 0) {
        render_panel_cable_gap_top(case_cable_gap_depth, -0.11);
    }
    if (panel_cable_gap_left > 0) {
        render_panel_cable_gap_left(case_cable_gap_depth, -0.11);
    }
    if (panel_cable_gap_right > 0) {
        render_panel_cable_gap_right(case_cable_gap_depth, -0.11);
    }
}

module case_holes() {
    if (case_hole_left_top > 0) {
        translate([-0.11, case_hole_left_top_offset + panel_border_top + case_inner_padding_top - 0.11, -0.11 + case_hole_top_depth])
        cube([
            panel_border_left + case_inner_padding_left + 0.22,
            case_hole_left_top + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }
    if (case_hole_left_bottom > 0) {
        translate([-0.11, - case_hole_left_bottom_offset + panel_border_top + panel_height_with_clearance - case_inner_padding_bottom - case_hole_left_bottom - 0.11, -0.11 + case_hole_top_depth])
        cube([
            panel_border_left + case_inner_padding_left + 0.22,
            case_hole_left_bottom + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }
    if (case_hole_right_top > 0) {
        translate([panel_width_with_clearance + panel_border_left - case_inner_padding_right - 0.11, case_hole_right_top_offset + panel_border_top + case_inner_padding_top - 0.11, -0.11 + case_hole_top_depth])
        cube([
            panel_border_right + case_inner_padding_right + 0.22,
            case_hole_right_top + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }
    if (case_hole_right_bottom > 0) {
        translate([panel_width_with_clearance + panel_border_left - case_inner_padding_right - 0.11, - case_hole_right_bottom_offset + panel_border_top + panel_height_with_clearance - case_inner_padding_bottom - case_hole_right_bottom - 0.11, -0.11 + case_hole_top_depth])
        cube([
            panel_border_right + case_inner_padding_right + 0.22,
            case_hole_right_bottom + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }
    if (case_hole_top_left > 0) {
        translate([case_hole_top_left_offset + panel_border_left + case_inner_padding_left - 0.11, -0.11, -0.11 + case_hole_top_depth])
        cube([
            case_hole_top_left + 0.22,
            panel_border_top + case_inner_padding_top + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }
    if (case_hole_top_right > 0) {
        translate([- case_hole_top_right_offset + panel_border_left + panel_width_with_clearance - case_inner_padding_right - case_hole_top_right - 0.11, -0.11, -0.11 + case_hole_top_depth])
        cube([
            case_hole_top_right + 0.22,
            panel_border_top + case_inner_padding_top + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }
    if (case_hole_bottom_left > 0) {
        translate([case_hole_bottom_left_offset + panel_border_left + case_inner_padding_left - 0.11, panel_border_top + panel_height_with_clearance - case_inner_padding_bottom - 0.11, -0.11 + case_hole_top_depth])
        cube([
            case_hole_bottom_left + 0.22,
            panel_border_top + case_inner_padding_top + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }
    if (case_hole_bottom_right > 0) {
        translate([-case_hole_bottom_right_offset + panel_border_left + panel_width_with_clearance - case_inner_padding_right - case_hole_bottom_right - 0.11, panel_border_top + panel_height_with_clearance - case_inner_padding_bottom - 0.11, -0.11 + case_hole_top_depth])
        cube([
            case_hole_bottom_right + 0.22,
            panel_border_top + case_inner_padding_top + 0.22,
            case_depth - case_hole_top_depth - case_hole_bottom_depth + 0.22
        ]);
    }   
}

module case() {
    // Cut out inner cylinders for screws
    difference() {
        union() {
            if (case_center_support_vertical || case_center_support_horizontal) {
                difference() {
                    color(case_color)
                    union() {
                        // Center support
                        if (case_center_support_horizontal) {
                            cubeWithAngledTopBottom(
                                loc=[frame_full_width / 4, frame_full_height / 2 - (case_center_support_width / 2), 0],
                                size=[frame_full_width / 6, case_center_support_width, case_depth],
                                bottom=(view_mode == "print_vertical")
                            );
                            cubeWithAngledTopBottom(
                                loc=[frame_full_width / 4 + frame_full_width / 3, frame_full_height / 2 - (case_center_support_width / 2), 0],
                                size=[frame_full_width / 6, case_center_support_width, case_depth],
                                bottom=(view_mode == "print_vertical")
                            );
                        }

                        if (case_center_support_vertical) {
                            cubeWithAngledTopBottom(
                                loc=[frame_full_width / 2 - (case_center_support_width / 2), frame_full_height / 4, 0],
                                size=[case_center_support_width, frame_full_height / 6, case_depth],
                                bottom=(view_mode == "print_vertical")
                            );
                            cubeWithAngledTopBottom(
                                loc=[frame_full_width / 2 - (case_center_support_width / 2), frame_full_height / 4 + frame_full_height / 3, 0],
                                size=[case_center_support_width, frame_full_height / 6, case_depth],
                                bottom=(view_mode == "print_vertical")
                            );
                        }
                    };
                }
            };

            if (usb_cutout) {
                color(case_color)
                cubeWithAngledTopBottom(
                    loc=[
                        usb_cutout_x, 
                        usb_cutout_y,
                        back_depth + case_depth - (usb_cutout_box_depth + usb_cutout_back_wall_thickness),
                    ], 
                    size=[
                        usb_cutout_box_width + usb_cutout_left_wall_thickness + usb_cutout_right_wall_thickness, 
                        usb_cutout_box_height + usb_cutout_top_wall_thickness + usb_cutout_bottom_wall_thickness,
                        usb_cutout_box_depth + usb_cutout_back_wall_thickness
                    ], 
                    top=(view_mode=="print_vertical" && usb_cutout_hole_position != "top") || usb_cutout_hole_position == "back",
                    bottom=(view_mode=="print_vertical" && usb_cutout_hole_position != "bottom") || usb_cutout_hole_position == "back"
                );

                if (sd_card_in_usb_cutout) {
                    color(case_color)
                    translate([usb_sd_card_x_position, usb_sd_card_y_position, 0])
                    rotate([0, 0, sd_card_in_usb_cutout_side == "left" ? 90 : -90])
                    sdCardAdapterBase();
                }
            }

            if (sd_cutout) {
                color(case_color)
                cubeWithAngledTopBottom(
                    loc=[
                        sd_cutout_x, 
                        sd_cutout_y,
                        back_depth + case_depth - (sd_cutout_box_depth + sd_cutout_back_wall_thickness),
                    ], 
                    size=[
                        sd_cutout_box_width + sd_cutout_left_wall_thickness + sd_cutout_right_wall_thickness, 
                        sd_cutout_box_height + sd_cutout_top_wall_thickness + sd_cutout_bottom_wall_thickness,
                        sd_cutout_box_depth + sd_cutout_back_wall_thickness
                    ], 
                    top=(view_mode=="print_vertical" && sd_cutout_hole_position != "top") || sd_cutout_hole_position == "back",
                    bottom=(view_mode=="print_vertical" && sd_cutout_hole_position != "bottom") || sd_cutout_hole_position == "back"
                );
            }

            for (h = hanging_holes) {
                let (
                    hanging_hole_edge = h[0],
                    hanging_hole_offset_percentage = h[1],
                    hanging_hole_pos = hanging_hole_position(hanging_hole_edge, hanging_hole_offset_percentage),
                    hanging_hole_x = hanging_hole_pos[0],
                    hanging_hole_y = hanging_hole_pos[1]
                ) {
                    color(case_color)
                    cubeWithAngledTopBottom(
                        loc=[
                            hanging_hole_x - hanging_hole_wall_thickness,
                            hanging_hole_y - hanging_hole_wall_thickness,
                            back_depth + case_depth - min(back_depth + case_depth, hanging_hole_depth)
                        ],
                        size=[
                            hanging_hole_edge == "top" || hanging_hole_edge == "bottom" 
                                ? hanging_hole_box_width + hanging_hole_wall_thickness * 2
                                : hanging_hole_box_height + hanging_hole_wall_thickness * 2, 
                            hanging_hole_edge == "top" || hanging_hole_edge == "bottom" 
                                ? hanging_hole_box_height + hanging_hole_wall_thickness * 2
                                : hanging_hole_box_width + hanging_hole_wall_thickness * 2, 
                            min(back_depth + case_depth, hanging_hole_depth)
                        ],
                        bottom=(view_mode == "print_vertical")
                    );
                }
            }
            if (pi_pinholes) {
                color(case_color)
                piPinholes();
            }
            if (extra_pin_mounts_enabled || extra_pinholes) {
                color(case_color)
                extraPinMounts();
            }

            // Cut out a piece of the cube
            color(case_color)
            caseBody();

            if (thick_border && view_mode != "only_case") {
                color(thick_border_color)
                caseThickBorder();
            }
        };
        
        // Cut holes to the back
        for (c = screw_positions) {
            // Screw thread hole that goes all the way
            color(case_color)
            translate([c[0], c[1], - 0.11])
            cylinder(d = case_screw_hole_thread_diameter,
                     h = case_depth + back_depth + 0.11);
            
            hole_depth = case_depth + back_depth - case_screw_hole_insert_depth - case_screw_hole_floor_depth + 0.11;

            if (hole_depth > case_screw_hole_diameter - case_screw_hole_thread_diameter) {
                // Cylinder hole from back to insert - chamfer
                color(case_color)
                translate([c[0], c[1], case_screw_hole_insert_depth + case_screw_hole_floor_depth]) // Solid border around the screw hole
                cylinder(d2 = case_screw_hole_diameter,
                        d1 = case_screw_hole_thread_diameter,
                        h = case_screw_hole_diameter - case_screw_hole_thread_diameter); // Hole for the screw thread
                // Cylinder hole from back to insert - tube
                color(case_color)
                translate([c[0], c[1], case_screw_hole_insert_depth + case_screw_hole_floor_depth + (case_screw_hole_diameter - case_screw_hole_thread_diameter) - 0.01]) // Solid border around the screw hole
                cylinder(d = case_screw_hole_diameter,
                        h = hole_depth - (case_screw_hole_diameter - case_screw_hole_thread_diameter) + 0.11); // Hole for the screw thread
            } else {
                // Cylinder hole from back to insert
                color(case_color)
                translate([c[0], c[1], case_screw_hole_insert_depth + case_screw_hole_floor_depth]) // Solid border around the screw hole
                cylinder(d2 = case_screw_hole_diameter,
                        d1 = case_screw_hole_thread_diameter,
                        h = hole_depth); // Hole for the screw thread
            }
            
            // Cylinder hole from front to insert
            color(case_color)
            translate([c[0], c[1], - 0.11]) // Solid border around the screw hole
            cylinder(d = case_screw_hole_diameter,
                     h = case_screw_hole_insert_depth + 0.11); // Hole for the screw thread
        }

        if ((extra_pin_mounts_enabled || extra_pinholes) && extra_pin_mounts_hole_diameter > 0) {
            color(case_color)
            extraPinMountHoles();
        }

        if (usb_cutout) {
            // Cutout into box
            color(case_color)
            cubeWithLeftRightGapBridge(
                loc=[
                    usb_cutout_x + usb_cutout_left_wall_thickness, 
                    usb_cutout_y + usb_cutout_top_wall_thickness,
                    back_depth + case_depth - usb_cutout_box_depth,
                ], 
                size=[
                    usb_cutout_box_width, 
                    usb_cutout_box_height, 
                    usb_cutout_box_depth + 0.11
                ], 
                top=(view_mode=="print_vertical" && usb_cutout_hole_position != "top") || usb_cutout_hole_position == "back",
                bottom=(view_mode=="print_vertical" && usb_cutout_hole_position != "bottom") || usb_cutout_hole_position == "back"
            );

            if (sd_card_in_usb_cutout) {
                color(case_color)
                translate([usb_sd_card_x_position, usb_sd_card_y_position, 0])
                rotate([0, 0, sd_card_in_usb_cutout_side == "left" ? 90 : -90])
                sdCardAdapterCutout();
            }

            // Hole into what's remaining
            if (usb_cutout_hole_position == "left" || usb_cutout_hole_position == "right") {
                color(case_color)
                translate([
                    (frame_full_width - usb_cutout_box_width - usb_cutout_left_wall_thickness - usb_cutout_right_wall_thickness) * usb_cutout_offset_x_percentage / 100 - 0.11 + (usb_cutout_hole_position == "right" ? usb_cutout_box_width + usb_cutout_left_wall_thickness : 0), 
                    (frame_full_height - usb_cutout_box_height - usb_cutout_top_wall_thickness - usb_cutout_bottom_wall_thickness) * usb_cutout_offset_y_percentage / 100 + usb_cutout_top_wall_thickness + (usb_cutout_box_height - usb_cutout_hole_width) / 2,
                    back_depth + case_depth - usb_cutout_box_depth + (usb_cutout_box_depth - usb_cutout_hole_height) / 2 + usb_cutout_hole_offset,
                ])
                cube([
                    (usb_cutout_hole_position == "left" ? usb_cutout_left_wall_thickness : usb_cutout_right_wall_thickness) + 0.22, 
                    usb_cutout_hole_width, 
                    usb_cutout_hole_height
                ]);
            } else if (usb_cutout_hole_position == "top" || usb_cutout_hole_position == "bottom") {
                color(case_color)
                translate([
                    (frame_full_width - usb_cutout_box_width - usb_cutout_left_wall_thickness - usb_cutout_right_wall_thickness) * usb_cutout_offset_x_percentage / 100 + usb_cutout_left_wall_thickness + (usb_cutout_box_width - usb_cutout_hole_width) / 2, 
                    (frame_full_height - usb_cutout_box_height - usb_cutout_top_wall_thickness - usb_cutout_bottom_wall_thickness) * usb_cutout_offset_y_percentage / 100 - 0.11  + (usb_cutout_hole_position == "bottom" ? usb_cutout_box_height + usb_cutout_top_wall_thickness : 0),
                    back_depth + case_depth - usb_cutout_box_depth + (usb_cutout_box_depth - usb_cutout_hole_height) / 2 + usb_cutout_hole_offset,
                ])
                cube([
                    usb_cutout_hole_width, 
                    (usb_cutout_hole_position == "top" ? usb_cutout_top_wall_thickness : usb_cutout_bottom_wall_thickness) + 0.22, 
                    usb_cutout_hole_height
                ]);
            } else if (usb_cutout_hole_position == "back") {
                color(case_color)
                translate([
                    (frame_full_width - usb_cutout_box_width - usb_cutout_left_wall_thickness - usb_cutout_right_wall_thickness) * usb_cutout_offset_x_percentage / 100 + usb_cutout_left_wall_thickness + (usb_cutout_box_width - usb_cutout_hole_width) / 2, 
                    (frame_full_height - usb_cutout_box_height - usb_cutout_top_wall_thickness - usb_cutout_bottom_wall_thickness) * usb_cutout_offset_y_percentage / 100 - 0.11 + (usb_cutout_box_height - usb_cutout_hole_height) / 2 + usb_cutout_top_wall_thickness,
                    back_depth + case_depth - usb_cutout_box_depth - usb_cutout_back_wall_thickness - 0.11,
                ])
                cube([
                    usb_cutout_hole_width, 
                    usb_cutout_hole_height,
                    usb_cutout_back_wall_thickness + 0.22, 
                ]);
            }
        }

        if (hole_cutout) {
            // Cutout into box
            color(case_color)
            cubeWithLeftRightGapBridge(
                loc=[
                    hole_cutout_x, 
                    hole_cutout_y,
                    back_depth + case_depth - hole_cutout_box_depth,
                ], 
                size=[
                    hole_cutout_box_width, 
                    hole_cutout_box_height, 
                    hole_cutout_box_depth + 0.11
                ], 
                top=false,
                bottom=false
            );
        }

        if (sd_cutout) {
            // Cutout into box
            color(case_color)
            cubeWithLeftRightGapBridge(
                loc=[
                    sd_cutout_x + sd_cutout_left_wall_thickness, 
                    sd_cutout_y + sd_cutout_top_wall_thickness,
                    back_depth + case_depth - sd_cutout_box_depth,
                ], 
                size=[
                    sd_cutout_box_width, 
                    sd_cutout_box_height, 
                    sd_cutout_box_depth + 0.11
                ], 
                top=(view_mode=="print_vertical" && sd_cutout_hole_position != "top") || sd_cutout_hole_position == "back",
                bottom=(view_mode=="print_vertical" && sd_cutout_hole_position != "bottom") || sd_cutout_hole_position == "back"
            );

            // Hole into what's remaining
            if (sd_cutout_hole_position == "left" || sd_cutout_hole_position == "right") {
                color(case_color)
                translate([
                    (frame_full_width - sd_cutout_box_width - sd_cutout_left_wall_thickness - sd_cutout_right_wall_thickness) * sd_cutout_offset_x_percentage / 100 - 0.11 + (sd_cutout_hole_position == "right" ? sd_cutout_box_width + sd_cutout_left_wall_thickness : 0), 
                    (frame_full_height - sd_cutout_box_height - sd_cutout_top_wall_thickness - sd_cutout_bottom_wall_thickness) * sd_cutout_offset_y_percentage / 100 + sd_cutout_top_wall_thickness + (sd_cutout_box_height - sd_cutout_hole_width) / 2,
                    back_depth + case_depth - sd_cutout_box_depth + (sd_cutout_box_depth - sd_cutout_hole_height) / 2 + sd_cutout_hole_offset,
                ])
                cube([
                    (sd_cutout_hole_position == "left" ? sd_cutout_left_wall_thickness : sd_cutout_right_wall_thickness) + 0.22, 
                    sd_cutout_hole_width, 
                    sd_cutout_hole_height
                ]);
            } else if (sd_cutout_hole_position == "top" || sd_cutout_hole_position == "bottom") {
                color(case_color)
                translate([
                    (frame_full_width - sd_cutout_box_width - sd_cutout_left_wall_thickness - sd_cutout_right_wall_thickness) * sd_cutout_offset_x_percentage / 100 + sd_cutout_left_wall_thickness + (sd_cutout_box_width - sd_cutout_hole_width) / 2, 
                    (frame_full_height - sd_cutout_box_height - sd_cutout_top_wall_thickness - sd_cutout_bottom_wall_thickness) * sd_cutout_offset_y_percentage / 100 - 0.11  + (sd_cutout_hole_position == "bottom" ? sd_cutout_box_height + sd_cutout_top_wall_thickness : 0),
                    back_depth + case_depth - sd_cutout_box_depth + (sd_cutout_box_depth - sd_cutout_hole_height) / 2 + sd_cutout_hole_offset,
                ])
                cube([
                    sd_cutout_hole_width, 
                    (sd_cutout_hole_position == "top" ? sd_cutout_top_wall_thickness : sd_cutout_bottom_wall_thickness) + 0.22, 
                    sd_cutout_hole_height
                ]);
            } else if (sd_cutout_hole_position == "back") {
                color(case_color)
                translate([
                    (frame_full_width - sd_cutout_box_width - sd_cutout_left_wall_thickness - sd_cutout_right_wall_thickness) * sd_cutout_offset_x_percentage / 100 + sd_cutout_left_wall_thickness + (sd_cutout_box_width - sd_cutout_hole_width) / 2, 
                    (frame_full_height - sd_cutout_box_height - sd_cutout_top_wall_thickness - sd_cutout_bottom_wall_thickness) * sd_cutout_offset_y_percentage / 100 - 0.11 + (sd_cutout_box_height - sd_cutout_hole_height) / 2 + sd_cutout_top_wall_thickness,
                    back_depth + case_depth - sd_cutout_box_depth - sd_cutout_back_wall_thickness - 0.11,
                ])
                cube([
                    sd_cutout_hole_width, 
                    sd_cutout_hole_height,
                    sd_cutout_back_wall_thickness + 0.22, 
                ]);
            }
        }

        for (h = hanging_holes) {
            let (
                hanging_hole_edge = h[0],
                hanging_hole_offset_percentage = h[1],
                hanging_hole_pos = hanging_hole_position(hanging_hole_edge, hanging_hole_offset_percentage),
                hanging_hole_x = hanging_hole_pos[0],
                hanging_hole_y = hanging_hole_pos[1]
            ) {
                color(case_color)
                cubeWithAngledTopBottom(
                    loc=[
                        hanging_hole_x,
                        hanging_hole_y,
                        back_depth + case_depth - min(back_depth + case_depth, hanging_hole_depth) + hanging_hole_wall_thickness
                    ],
                    size=[
                        hanging_hole_edge == "top" || hanging_hole_edge == "bottom"
                            ? hanging_hole_box_width
                            : hanging_hole_box_height,
                        hanging_hole_edge == "top" || hanging_hole_edge == "bottom"
                            ? hanging_hole_box_height
                            : hanging_hole_box_width,
                        min(back_depth + case_depth, hanging_hole_depth) - hanging_hole_wall_thickness * 2
                    ],
                    bottom=(view_mode == "print_vertical")
                );

                // Big cyclinter hole
                color(case_color)
                translate([
                    hanging_hole_edge == "top" || hanging_hole_edge == "bottom"
                        ? frame_full_width / 2
                        : hanging_hole_edge == "left"
                            ? hanging_hole_x + hanging_hole_box_height * 0.75
                            : hanging_hole_x + hanging_hole_box_height * 0.25,
                    hanging_hole_edge == "left" || hanging_hole_edge == "right"
                        ? frame_full_height / 2
                        : hanging_hole_edge == "top" 
                            ? hanging_hole_y + hanging_hole_box_height * 0.75
                            : hanging_hole_y + hanging_hole_box_height * 0.25,
                    case_depth - 0.11
                ])
                rotate([0, 0, 90])
                cylinder(d = hanging_hole_large_diameter, h = back_depth + 0.21);

                // Small cyclinter hole
                color(case_color)
                translate([
                    hanging_hole_edge == "top" || hanging_hole_edge == "bottom"
                        ? frame_full_width / 2
                        : hanging_hole_edge == "left"
                            ? hanging_hole_x + hanging_hole_box_height * 0.25
                            : hanging_hole_x + hanging_hole_box_height * 0.75,
                    hanging_hole_edge == "left" || hanging_hole_edge == "right"
                        ? frame_full_height / 2
                        : hanging_hole_edge == "top" 
                            ? hanging_hole_y + hanging_hole_box_height * 0.25
                            : hanging_hole_y + hanging_hole_box_height * 0.75,
                    case_depth - 0.11
                ])
                rotate([0, 0, 90])
                cylinder(d = hanging_hole_small_diameter, h = back_depth + 0.21);

                // Box connecitng the two
                color(case_color)
                translate([
                    hanging_hole_edge == "top" || hanging_hole_edge == "bottom"
                        ? frame_full_width / 2 - hanging_hole_small_diameter / 2
                        : hanging_hole_x + hanging_hole_box_height * 0.25,
                    hanging_hole_edge == "left" || hanging_hole_edge == "right"
                        ? frame_full_height / 2 - hanging_hole_small_diameter / 2
                        : hanging_hole_y + hanging_hole_box_height * 0.25,
                    case_depth - 0.11
                ])
                cube([
                    hanging_hole_edge == "top" || hanging_hole_edge == "bottom"
                        ? hanging_hole_small_diameter
                        : hanging_hole_large_diameter,
                    hanging_hole_edge == "left" || hanging_hole_edge == "right"
                        ? hanging_hole_small_diameter
                        : hanging_hole_large_diameter,
                    back_depth + 0.21
                ]);
            }
        }

        if (rear_cooling) {
            color(case_color)
            rearCooling();
        }

        if (pi_pinholes && pi_pinholes_cooling) {
            color(case_color)
            piPinholesCooling();
        }

        if (backplate_access_holes) {
            color(case_color)
            backplateAccessHoles();
        }

        if (reverse_border_thickness) {
            color(case_color)
            reverse_border_case_side_button_clearance();
        } else {
            color(case_color)
            sideButtonHoles();
        }

        reverse_border_case_cutout();
    }
}

module reverse_border_case_cutout() {
    if (reverse_border_thickness && case_depth > 0) {
        reverse_border_case_cutout_strips();
        reverse_border_case_screw_cutouts();
    }
}

module reverse_border_case_cutout_strips() {
    reverse_border_case_cutout_cube(
        -reverse_border_gap,
        -reverse_border_gap,
        reverse_border_left_width + reverse_border_gap * 2,
        frame_full_height + reverse_border_gap * 2
    );
    reverse_border_case_cutout_cube(
        frame_full_width - reverse_border_right_width - reverse_border_gap,
        -reverse_border_gap,
        reverse_border_right_width + reverse_border_gap * 2,
        frame_full_height + reverse_border_gap * 2
    );
    reverse_border_case_cutout_cube(
        -reverse_border_gap,
        -reverse_border_gap,
        frame_full_width + reverse_border_gap * 2,
        reverse_border_top_width + reverse_border_gap * 2
    );
    reverse_border_case_cutout_cube(
        -reverse_border_gap,
        frame_full_height - reverse_border_bottom_width - reverse_border_gap,
        frame_full_width + reverse_border_gap * 2,
        reverse_border_bottom_width + reverse_border_gap * 2
    );
}

module reverse_border_case_screw_cutouts() {
    intersection() {
        union() {
            for (c = screw_positions) {
                translate([c[0], c[1], -0.11])
                cylinder(
                    d = reverse_border_screw_post_diameter + reverse_border_gap * 2,
                    h = case_depth + 0.11
                );
            }
        }

        union() {
            reverse_border_case_cutout_strips();
        }
    }
}

module reverse_border_case_cutout_cube(x, y, width, height) {
    if (width > 0 && height > 0) {
        translate([x, y, -0.11])
        cube([
            width,
            height,
            case_depth + 0.11
        ]);
    }
}

module reverse_border_case_side_button_clearance() {
    let (
        clearance_width = side_button_width + side_button_base_border * 2 + side_button_hole_gap * 2,
        depth = case_depth + 0.11
    ) {
        for (side_button = side_buttons_left) {
            translate([
                -reverse_border_gap,
                side_button * frame_full_height - clearance_width / 2,
                -0.11
            ])
            cube([
                panel_border_left + case_inner_padding_left + reverse_border_gap,
                clearance_width,
                depth
            ]);
        }

        for (side_button = side_buttons_right) {
            translate([
                frame_full_width - panel_border_right - case_inner_padding_right,
                side_button * frame_full_height - clearance_width / 2,
                -0.11
            ])
            cube([
                panel_border_right + case_inner_padding_right + reverse_border_gap,
                clearance_width,
                depth
            ]);
        }

        for (side_button = side_buttons_top) {
            translate([
                side_button * frame_full_width - clearance_width / 2,
                -reverse_border_gap,
                -0.11
            ])
            cube([
                clearance_width,
                panel_border_top + case_inner_padding_top + reverse_border_gap,
                depth
            ]);
        }

        for (side_button = side_buttons_bottom) {
            translate([
                side_button * frame_full_width - clearance_width / 2,
                frame_full_height - panel_border_bottom - case_inner_padding_bottom,
                -0.11
            ])
            cube([
                clearance_width,
                panel_border_bottom + case_inner_padding_bottom + reverse_border_gap,
                depth
            ]);
        }
    }
}

/*****************************************************************************/
/*                             Kickstand                                     */
/*****************************************************************************/

function angled_face_run(depth, angle) =
    depth / tan(max(min(angle, 89.9), 0.1));

module caseWithKickstand() {
    hinge_real_depth = kickstand_depth - kickstand_wall_thickness - kickstand_hinge_wall_padding;
    hinge_top_offset = kickstand_wall_thickness + kickstand_depth / 2 + kickstand_hinge_top_extra_leverage;
    hinge_start = [
        frame_full_width / 2 - kickstand_full_width / 2 + 0.11, 
        frame_full_height - kickstand_bottom_start - kickstand_height + hinge_top_offset,
        case_depth + back_depth - hinge_real_depth / 2
    ];
    leg_x_starts_full = [
        (frame_full_width - kickstand_full_width) / 2,
        (frame_full_width + kickstand_full_width) / 2 - kickstand_leg_full_width,
    ];
    leg_x_starts_hole = [
        leg_x_starts_full[0] + kickstand_wall_thickness,
        leg_x_starts_full[1] + kickstand_wall_thickness,
    ];
    leg_x_starts_leg = [
        leg_x_starts_hole[0] + kickstand_gap_thickness,
        leg_x_starts_hole[1] + kickstand_gap_thickness,
    ];
    leg_depth = kickstand_depth - kickstand_gap_thickness - kickstand_wall_thickness;
    leg_bridge_depth = leg_depth * 0.8;
    leg_bottom_run = angled_face_run(leg_depth, kickstand_leg_bottom_angle);
    leg_hole_depth = kickstand_depth - kickstand_wall_thickness + kickstand_gap_thickness + 0.11;
    leg_hole_bottom_run = angled_face_run(leg_hole_depth, kickstand_leg_bottom_angle);
    leg_yz = [
        frame_full_height - kickstand_bottom_start - kickstand_height + kickstand_gap_thickness + kickstand_wall_thickness,
        case_depth + back_depth - leg_depth
    ];

    leg_bridge_leg_y = frame_full_height - kickstand_bottom_start - kickstand_leg_bridge_offset - kickstand_leg_bridge_height * 2;
    leg_bridge_hole_y = leg_bridge_leg_y - kickstand_gap_thickness;
    leg_bridge_full_y = leg_bridge_leg_y - kickstand_wall_thickness;
    leg_top_height_full = kickstand_depth + kickstand_hinge_top_extra_leverage + kickstand_wall_thickness * 2 + kickstand_gap_thickness;
    sd_card_x_position = (sd_card_in_leg_side == "left" ? leg_x_starts_hole[0] + 0.11 : leg_x_starts_hole[1] + 0.11) + 
            (sd_card_in_leg_side_side == "left" ? 0 : kickstand_leg_width + kickstand_gap_thickness * 2 - 0.21);
    sd_card_y_position = sd_card_in_leg_y_percentage / 100 * kickstand_height + (frame_full_height - kickstand_height);

    // Make a hole in the case
    difference() {
        union() {
            case();
            // Protective box around the kickstand legs
            for (x = leg_x_starts_full) {
                // Long base leg
                color(case_color)
                cubeWithAngledTopBottom(
                    loc=[
                        x, 
                        frame_full_height - kickstand_bottom_start - kickstand_height, 
                        case_depth + back_depth - kickstand_depth - kickstand_gap_thickness
                    ],
                    size=[kickstand_leg_full_width, kickstand_height, kickstand_depth + kickstand_gap_thickness],
                    bottom=(view_mode == "print_vertical" && kickstand_bottom_start >= kickstand_depth),
                    top=true
                );
            }
    
            // Protective box around the leg bridge
            color(case_color)
            cubeWithAngledTopBottom(
                loc=[
                    leg_x_starts_full[0] + kickstand_leg_full_width - kickstand_wall_thickness * 2, 
                    leg_bridge_full_y + kickstand_depth - kickstand_wall_thickness, 
                    case_depth + back_depth - kickstand_depth
                ],
                size=[
                    kickstand_full_width - kickstand_leg_full_width * 2 + kickstand_wall_thickness * 4, 
                    kickstand_leg_bridge_height * 2 + kickstand_wall_thickness * 2 - kickstand_depth * 2, 
                    kickstand_depth
                ],
                top=true,
                bottom=true
            );
        }
        // Now the holes
        for (x = leg_x_starts_hole) {
            // Top thicker part - top extra cavity
            color(case_color)
            cubeWithAngledTopBottom(
                loc=[
                    x,
                    frame_full_height - kickstand_bottom_start - kickstand_height,
                    case_depth + back_depth - kickstand_depth + kickstand_gap_thickness
                ],
                size=[
                    kickstand_leg_width + 2 * kickstand_gap_thickness,
                    kickstand_wall_thickness + 0.11,
                    kickstand_depth - kickstand_wall_thickness * 2
                ],
                top=true
            );
            // Long base leg
            color(case_color)
            cubeWithLeftRightGapBridge(
                loc=[
                    x,
                    frame_full_height - kickstand_bottom_start - kickstand_height + kickstand_wall_thickness,
                    case_depth + back_depth - kickstand_depth - kickstand_gap_thickness + kickstand_wall_thickness
                ],
                size=[
                    kickstand_leg_width + 2 * kickstand_gap_thickness,
                    max(kickstand_height - 2 * kickstand_wall_thickness - leg_hole_bottom_run, 0.01),
                    leg_hole_depth
                ],
                bottom=true,
                gap_width=0.4,
                bottom_angle=kickstand_leg_bottom_angle
            );
        }

        // Leg bridge hole
        color(case_color)
        cubeWithAngledTopBottom(
            loc=[
                leg_x_starts_full[0] + kickstand_leg_full_width - kickstand_wall_thickness - 0.11, 
                leg_bridge_full_y + kickstand_depth, 
                case_depth + back_depth - kickstand_depth + kickstand_gap_thickness 
            ],
            size=[
                kickstand_full_width - kickstand_leg_full_width * 2 + kickstand_wall_thickness * 2 + 0.22, 
                kickstand_leg_bridge_height * 2 - kickstand_depth * 2 + kickstand_gap_thickness, 
                kickstand_depth - kickstand_gap_thickness + 0.11
            ],
            top=true,
            bottom=true
        );

        if (sd_card_in_leg) {
            color(case_color)
            translate([sd_card_x_position, sd_card_y_position, 0])
            rotate([0, 0, sd_card_in_leg_side_side == "left" ? 90 : -90])
            sdCardAdapterCutout();
        }
    }

    // The kickstand itself
    difference () {
        union () {
            // Legs
            for (index=[0:1]) {
                // Render the leg's top cylinder over the hinge
                color(kickstand_color)
                translate([leg_x_starts_leg[index], hinge_start[1], hinge_start[2]])
                rotate([90, 0, 90])
                cylinder(d = hinge_real_depth, h = kickstand_leg_width);

                // Render the large leg top
                color(kickstand_color)
                translate([leg_x_starts_leg[index], hinge_start[1], hinge_start[2]])
                rotate([kickstand_rotation, 0, 0])
                cubeWithAngledTopBottom(
                    loc=[
                        0, 
                        leg_yz[0] - hinge_start[1], 
                        case_depth + back_depth - (hinge_real_depth / 2) - hinge_start[2]
                    ],
                    size=[
                        kickstand_leg_width, 
                        hinge_top_offset,
                        hinge_real_depth / 2
                    ]
                );

                // Render the large leg
                difference() {
                    color(kickstand_color)
                    translate([leg_x_starts_leg[index], hinge_start[1], hinge_start[2]])
                    rotate([kickstand_rotation, 0, 0])
                    cubeWithAngledTopBottom(
                        loc=[
                            0, 
                            leg_yz[0] - hinge_start[1] + hinge_top_offset - kickstand_hinge_diameter - kickstand_leg_bottom_padding,
                            leg_yz[1] - hinge_start[2]
                        ],
                        size=[
                            kickstand_leg_width, 
                            max(kickstand_height - kickstand_gap_thickness - kickstand_wall_thickness * 2 - kickstand_gap_thickness - hinge_top_offset + kickstand_hinge_diameter - leg_bottom_run - kickstand_leg_bottom_padding, 0.01),
                            leg_depth
                        ],
                        bottom=true,
                        bottom_angle=kickstand_leg_bottom_angle
                    );
                    if (kickstand_inner_snap) {
                        color(kickstand_color)
                        translate([leg_x_starts_leg[index] + (index == (kickstand_inner_snap_reverse ? 1 : 0) ? kickstand_leg_width : 0), hinge_start[1], hinge_start[2] - kickstand_gap_thickness])
                        rotate([kickstand_rotation, 0, 0])
                        color(kickstand_color)
                        translate([0, kickstand_inner_snap_height_percentage / 100 * (kickstand_height - kickstand_gap_thickness - kickstand_wall_thickness * 2 - kickstand_gap_thickness - hinge_top_offset + kickstand_hinge_diameter - leg_depth), 0])
                        sphere(d = kickstand_inner_snap_radius_hole * 2);
                    }
                }
            }
            // Render the leg bridge
            color(kickstand_color)
            translate([0, hinge_start[1], hinge_start[2]])
            rotate([kickstand_rotation, 0, 0])
            cubeWithAngledTopBottom(
                loc=[
                    leg_x_starts_leg[0] + kickstand_leg_width, 
                    leg_bridge_leg_y - hinge_start[1] + leg_bridge_depth,
                    case_depth + back_depth - leg_bridge_depth - hinge_start[2]
                ],
                size=[
                    kickstand_width - 2 * kickstand_leg_width, 
                    max(kickstand_leg_bridge_height - hinge_real_depth - leg_bridge_depth, 0), 
                    leg_bridge_depth
                ],
                top=true,
                bottom=true
            );

            if (sd_card_in_leg) {
                color(case_color)
                translate([sd_card_x_position, sd_card_y_position, 0])
                rotate([0, 0, sd_card_in_leg_side_side == "left" ? 90 : -90])
                sdCardAdapterBase();
            }
        }

        // Render an empty cylinder inside the top cylinder, where the hinge will go through
        color(kickstand_color)
        translate([leg_x_starts_leg[0] - 0.11, hinge_start[1], hinge_start[2]])
        rotate([90, 0, 90])
        cylinder(d = kickstand_hinge_diameter + kickstand_hinge_cylinder_gap * 2, h = kickstand_width + 0.22);
    }

    for (index=[0:1]) {
        // Render a cylinder as a hinge
        color(case_color)
        translate([leg_x_starts_full[index], hinge_start[1], hinge_start[2]])
        rotate([90, 0, 90])
        cylinder(d = kickstand_hinge_diameter, h = kickstand_leg_full_width - 0.22);

        // Render the snaps for the legs
        if (kickstand_inner_snap) {
            color(case_color)
            translate([
                leg_x_starts_leg[index] + (index == (kickstand_inner_snap_reverse ? 1 : 0) ? kickstand_leg_width : 0) + (index == (kickstand_inner_snap_reverse ? 1 : 0) ? kickstand_inner_snap_gap : - kickstand_inner_snap_gap), 
                hinge_start[1] + kickstand_inner_snap_height_percentage / 100 * (kickstand_height - kickstand_gap_thickness - kickstand_wall_thickness * 2 - kickstand_gap_thickness - hinge_top_offset + kickstand_hinge_diameter - leg_depth), 
                hinge_start[2] - kickstand_gap_thickness
            ])
            color(kickstand_color)
            intersection() {
                sphere(d = kickstand_inner_snap_radius_ball * 2);
                translate([-kickstand_inner_snap_radius_hole * 2 + (index == (kickstand_inner_snap_reverse ? 1 : 0) ? 0 : +kickstand_inner_snap_radius_hole * 2), -kickstand_inner_snap_radius_hole * 2, -kickstand_inner_snap_radius_hole * 2])
                cube([kickstand_inner_snap_radius_hole * 2, kickstand_inner_snap_radius_hole * 4, kickstand_inner_snap_radius_hole * 4]);
            }
        }
    }
}

/*****************************************************************************/
/*                              Utility                                      */
/*****************************************************************************/


module filletBox(x, y, z, r = fillet_radius_in_use, fn=64) {
    // Optional sanity check (warn if the fillet is too large):
    if (x < 2*r || y < 2*r || z < 2*r) {
        echo("WARNING: fillet radius is too large for box dimensions!");
    }
    
    // Minkowski sum of a smaller cube + sphere(r)
    // => yields a final shape with filleted edges/corners.
    translate([r, r, r])  // Move the sphere to the corner of the cube
    minkowski() {
        // Create the “inner” cube, shrunk by 2*r in each dimension
        // so that after Minkowski we end up with full x, y, z overall.
        cube([x - 2*r, y - 2*r, z - 2*r], center = false);

        // Add the sphere that “rounds” the corners/edges
        sphere(r = r, $fn = fn);  // $fn=64 for smoother arcs
    }
}

module filletBoxTop(x, y, z, r = fillet_radius_in_use, fn=64) {
    intersection() {
        cube([x, y, z]);
        filletBox(x, y, z * 2, r, fn);
    }
}

module filletBoxMiddle(x, y, z, r = fillet_radius_in_use, fn=64) {
    intersection() {
        cube([x, y, z]);
        translate([0, 0, -z])
        filletBox(x, y, z * 3, r, fn);
    }
}

module filletBoxBottom(x, y, z, r = fillet_radius_in_use, fn=64) {
    intersection() {
        cube([x, y, z]);
        translate([0, 0, -z])
        filletBox(x, y, z * 2, r, fn);
    }
}

module filletBoxLeft(x, y, z, r = fillet_radius_in_use, fn=64) {
    intersection() {
        cube([x, y, z]);
        filletBox(x * 2, y, z, r, fn);
    }
}

module filletBoxRight(x, y, z, r = fillet_radius_in_use, fn=64) {
    intersection() {
        cube([x, y, z]);
        translate([-x, 0, 0])
        filletBox(x * 2, y, z, r, fn);
    }
}

module filletBoxUp(x, y, z, r = fillet_radius_in_use, fn=64) {
    intersection() {
        cube([x, y, z]);
        filletBox(x, y * 2, z, r, fn);
    }
}

module filletBoxDown(x, y, z, r = fillet_radius_in_use, fn=64) {
    intersection() {
        cube([x, y, z]);
        translate([0, -y, 0])
        filletBox(x, y * 2, z, r, fn);
    }
}
module cubeWithAngledTopBottom(
    loc, size, 
    top=false, topReverse=false, 
    bottom=false, bottomReverse=false, 
    left=false, leftReverse=false, 
    right=false, rightReverse=false,
    top_angle=45,
    bottom_angle=45,
    left_angle=45,
    right_angle=45
) {
    top_run = angled_face_run(size[2], top_angle);
    bottom_run = angled_face_run(size[2], bottom_angle);
    left_run = angled_face_run(size[2], left_angle);
    right_run = angled_face_run(size[2], right_angle);

    translate(loc)
    cube(size);

    if (top) {
        translate([loc[0], loc[1] - (topReverse ? top_run : 0) + 0.01, loc[2]])
        rotate(topReverse ? [0,0,0] : [90,0,0])
        polyhedron(//pt 0        1        2        3        4        5
            points=[[0,0,0], [size[0],0,0], [size[0],top_run,0], [0,top_run,0], [0,top_run,size[2]], [size[0],top_run,size[2]]],
            faces=[[0,1,2,3],[5,4,3,2],[0,4,5,1],[0,3,4],[5,2,1]]
        );
    }
    if (bottom) {
        translate([loc[0], loc[1] + size[1] + (bottomReverse ? 0 : bottom_run) - 0.01, loc[2] + size[2]])
        rotate(bottomReverse ? [270,0,0] : [180,0,0])
        polyhedron(//pt 0        1        2        3        4        5
            points=[[0,0,0], [size[0],0,0], [size[0],bottom_run,0], [0,bottom_run,0], [0,bottom_run,size[2]], [size[0],bottom_run,size[2]]],
            faces=[[0,1,2,3],[5,4,3,2],[0,4,5,1],[0,3,4],[5,2,1]]
        );
    }
    if (left) {
        translate([loc[0] - (leftReverse ? 0 : left_run) + 0.01, loc[1], loc[2] + size[2]])
        rotate(leftReverse ? [270,0,90] : [180,0,90])
        polyhedron(//pt 0        1        2        3        4        5
            points=[[0,0,0], [size[1],0,0], [size[1],left_run,0], [0,left_run,0], [0,left_run,size[2]], [size[1],left_run,size[2]]],
            faces=[[0,1,2,3],[5,4,3,2],[0,4,5,1],[0,3,4],[5,2,1]]
        );
    }
    if (right) {
        translate([loc[0] + size[0] + (rightReverse ? 0 : right_run) - 0.01, loc[1] + size[1], loc[2] + size[2]])
        rotate(rightReverse ? [270,0,270] : [180,0,270])
        polyhedron(//pt 0        1        2        3        4        5
            points=[[0,0,0], [size[1],0,0], [size[1],right_run,0], [0,right_run,0], [0,right_run,size[2]], [size[1],right_run,size[2]],
            ],
            faces=[[0,1,2,3],[5,4,3,2],[0,4,5,1],[0,3,4],[5,2,1]]
        );
    }
}

module cubeWithLeftRightGapBridge(
    loc, size, 
    top=false, topReverse=false, 
    bottom=false, bottomReverse=false, 
    left=false, leftReverse=false, 
    right=false, rightReverse=false,
    gap_width = 0.4,
    top_angle=45,
    bottom_angle=45,
    left_angle=45,
    right_angle=45
) {
    if (size[0] < size[1]) {
        cubeWithAngledTopBottom(
            [loc[0] + gap_width, loc[1], loc[2]],
            [size[0] - gap_width * 2, size[1], size[2]], 
            top=false,
            bottom=false,
            left=false,
            right=false
        );
    } else {
        cubeWithAngledTopBottom(
            [loc[0], loc[1] + gap_width, loc[2]],
            [size[0], size[1] - gap_width * 2, size[2]], 
            top=false,
            bottom=false,
            left=false,
            right=false
        );
    }
    cubeWithAngledTopBottom(
        [loc[0], loc[1], loc[2] + gap_width],
        [size[0], size[1], size[2] - gap_width], 
        top=top,
        topReverse=topReverse,
        bottom=bottom,
        bottomReverse=bottomReverse,
        left=left,
        leftReverse=leftReverse,
        right=right,
        rightReverse=rightReverse,
        top_angle=top_angle,
        bottom_angle=bottom_angle,
        left_angle=left_angle,
        right_angle=right_angle
    );
}

sd_adapter_width = 24;
sd_adapter_height = 32;
sd_adapter_depth = 2.2;
sd_adapter_micro_width = 10.5;
sd_adapter_micro_hole_height = 3.3;

module sdCardAdapterCutout() {
    translate([-sd_adapter_width / 2, 0, case_depth - 2 * sd_adapter_depth])
    difference() {
        if (sd_card_in_leg_shape == "adapter") {
            union() {
                cube([sd_adapter_width, sd_adapter_height, sd_adapter_depth]);
                translate([(sd_adapter_width - sd_adapter_micro_width) / 2, 0, sd_adapter_depth - 0.01])
                cube([sd_adapter_micro_width, sd_adapter_micro_hole_height, sd_adapter_depth]);
            }
            translate([sd_adapter_width, sd_adapter_height - 3.49, -0.11])
            rotate([0, 0, 45])
            cube([5.5, 5.5, sd_adapter_depth + 0.21]);
        } else if (sd_card_in_leg_shape == "hole") {
            translate([(sd_adapter_width - sd_adapter_micro_width) / 2, 0, sd_adapter_depth - 0.01 - sd_card_in_leg_hole_offset])
            cube([sd_adapter_micro_width, sd_adapter_micro_hole_height, sd_adapter_depth]);
        }
    }
}

module sdCardAdapterBase() {
    if (sd_card_in_leg_shape == "adapter") {
        sd_adapter_buffer = 0.5;
        difference() {
            translate([-sd_adapter_width / 2 - sd_adapter_buffer, 0.01, case_depth - 2 * sd_adapter_depth + 0.01])
            cube([sd_adapter_width + 2 * sd_adapter_buffer, sd_adapter_height + sd_adapter_buffer, 2 * sd_adapter_depth]);

            sdCardAdapterCutout();
        }
    }
}

module coolingHoles(x_start, x_end, y_start, y_end, gap, radius = rear_cooling_radius, skip_corners = false) {
    let (
        x_extra = (x_end - x_start) % gap / 2,
        y_extra = (y_end - y_start) % gap / 2
    ) {
        for (x = [x_start : gap : x_end]) {
            for (y = [y_start : gap : y_end]) {
                if (
                    !skip_corners || !(
                        ((abs(x - x_start) < gap/2) || (abs(x - x_end) < gap/2)) &&
                        ((abs(y - y_start) < gap/2) || (abs(y - y_end) < gap/2))
                    )
                ) {
                    translate([x + x_extra, y + y_extra, -0.01 + case_depth])
                    cylinder(d = radius, h = back_depth + 0.12);
                }
            }
        }
    }
}

// Draw tiny holes into the back panel for cooling
module rearCooling() {
    rear_cooling_x_start = rear_cooling_x_start_percentage / 100 * frame_full_width;
    rear_cooling_x_end = rear_cooling_x_end_percentage / 100 * frame_full_width;
    rear_cooling_y_start = rear_cooling_y_start_percentage / 100 * frame_full_height;
    rear_cooling_y_end = rear_cooling_y_end_percentage / 100 * frame_full_height;

    coolingHoles(
        rear_cooling_x_start, rear_cooling_x_end,
        rear_cooling_y_start, rear_cooling_y_end,
        rear_cooling_gap
    );
}

function pi_pinholes_width_in_use() =
    pi_pinholes_orientation == "vertical" ? pi_pinholes_height : pi_pinholes_width;

function pi_pinholes_height_in_use() =
    pi_pinholes_orientation == "vertical" ? pi_pinholes_width : pi_pinholes_height;

function pi_pinholes_origin() =
    let (
        width = pi_pinholes_width_in_use(),
        height = pi_pinholes_height_in_use()
    ) [
        pi_pinholes_x_percentage / 100 * (frame_full_width - width),
        pi_pinholes_y_percentage / 100 * (frame_full_height - height)
    ];

function pi_relative_anchor_point(anchor, anchor_percentage) =
    let (
        width = pi_pinholes_width_in_use(),
        height = pi_pinholes_height_in_use(),
        origin = pi_pinholes_origin(),
        left = origin[0],
        right = origin[0] + width,
        bottom = origin[1],
        top = origin[1] + height,
        center_x = origin[0] + width / 2,
        center_y = origin[1] + height / 2
    )
    anchor == "percentage" ? [
        anchor_percentage[0] / 100 * frame_full_width,
        anchor_percentage[1] / 100 * frame_full_height
    ] :
    anchor == "top-left" ? [left, top] :
    anchor == "top" ? [center_x, top] :
    anchor == "top-right" ? [right, top] :
    anchor == "left" ? [left, center_y] :
    anchor == "right" ? [right, center_y] :
    anchor == "bottom-left" ? [left, bottom] :
    anchor == "bottom" ? [center_x, bottom] :
    anchor == "bottom-right" ? [right, bottom] :
    [center_x, center_y];

function extra_pin_mounts_use_legacy() =
    !extra_pin_mounts_enabled && extra_pinholes;

function extra_pin_mounts_in_use() =
    extra_pin_mounts_use_legacy() ? extra_pinholes_offsets : extra_pin_mounts;

function extra_pin_mounts_anchor_point() =
    pi_relative_anchor_point(
        extra_pin_mounts_use_legacy() ? extra_pinholes_anchor : extra_pin_mounts_anchor,
        extra_pin_mounts_use_legacy() ? extra_pinholes_anchor_percentage : extra_pin_mounts_anchor_percentage
    );

function extra_pinholes_anchor_point() =
    extra_pin_mounts_anchor_point();

function backplate_access_holes_anchor_point() =
    pi_relative_anchor_point(backplate_access_holes_anchor, backplate_access_holes_anchor_percentage);

module piPinholes() {
    let (
        width = pi_pinholes_width_in_use(),
        height = pi_pinholes_height_in_use(),
        origin = pi_pinholes_origin()
    ) {
        pin_holes_x = origin[0];
        pin_holes_y = origin[1];

        pin_hole_locations = [
            [pin_holes_x, pin_holes_y],
            [pin_holes_x + width, pin_holes_y],
            [pin_holes_x, pin_holes_y + height],
            [pin_holes_x + width, pin_holes_y + height]
        ];
        for (loc = pin_hole_locations) {
            pinHole(loc[0], loc[1]);
        }
    }
}

module pinHole(
    x,
    y,
    pin_diameter = pi_pinholes_diameter,
    spacer_diameter = pi_pinholes_spacer,
    spacer_height = pi_pinholes_spacer_height,
    pin_enabled = true
) {
    if (pin_enabled && pin_diameter > 0) {
        translate([x, y, -0.11])
        cylinder(d = pin_diameter, h = case_depth + 0.22);
    }
    if (spacer_diameter > 0 && spacer_height > 0) {
        translate([x, y, case_depth - spacer_height - 0.11])
        cylinder(d = spacer_diameter, h = spacer_height + 0.22);
    }
}

function extra_pin_mount_value(mount, index, default_value) =
    len(mount) > index ? (mount[index] == undef ? default_value : mount[index]) : default_value;

function extra_pin_mount_pin_diameter(mount) =
    extra_pin_mount_value(mount, 2, pi_pinholes_diameter);

function extra_pin_mount_spacer_diameter(mount) =
    extra_pin_mount_value(mount, 3, pi_pinholes_spacer);

function extra_pin_mount_spacer_height(mount) =
    extra_pin_mount_value(mount, 4, pi_pinholes_spacer_height);

function extra_pin_mount_pin_enabled(mount) =
    extra_pin_mount_value(mount, 5, true);

module extraPinMounts() {
    let (anchor = extra_pin_mounts_anchor_point()) {
        for (mount = extra_pin_mounts_in_use()) {
            pinHole(
                anchor[0] + mount[0],
                anchor[1] + mount[1],
                extra_pin_mount_pin_diameter(mount),
                extra_pin_mount_spacer_diameter(mount),
                extra_pin_mount_spacer_height(mount),
                extra_pin_mount_pin_enabled(mount)
            );
        }
    }
}

module extraPinMountHoles() {
    let (anchor = extra_pin_mounts_anchor_point()) {
        for (mount = extra_pin_mounts_in_use()) {
            let (spacer_height = extra_pin_mount_spacer_height(mount)) {
                if (spacer_height > 0) {
                    translate([
                        anchor[0] + mount[0],
                        anchor[1] + mount[1],
                        case_depth - spacer_height - 0.11
                    ])
                    cylinder(d = extra_pin_mounts_hole_diameter, h = spacer_height + 0.11);
                }
            }
        }
    }
}

module extraPinHoles() {
    extraPinMounts();
}

module backplateAccessHole(x, y) {
    translate([x, y, case_depth - 0.11])
    cylinder(d = backplate_access_holes_diameter, h = back_depth + 0.22);
}

module backplateAccessHoles() {
    let (anchor = backplate_access_holes_anchor_point()) {
        for (offset = backplate_access_holes_offsets) {
            backplateAccessHole(
                anchor[0] + offset[0],
                anchor[1] + offset[1]
            );
        }
    }
}

module piPinholesCooling() {
    let (
        width = pi_pinholes_width_in_use(),
        height = pi_pinholes_height_in_use(),
        origin = pi_pinholes_origin()
    ) {
        pin_holes_x = origin[0];
        pin_holes_y = origin[1];

        coolingHoles(
            pin_holes_x, pin_holes_x + width,
            pin_holes_y, pin_holes_y + height,
            pi_pinholes_cooling_gap,
            skip_corners=true
        );
    }
}

module sideButtonHoles() {
    let(thick_border_add = (thick_border ? thick_border_width : 0)) {
        // side buttons left
        for (side_button = side_buttons_left) {
            translate([
                -side_button_extrude - thick_border_add,
                side_button * frame_full_height - side_button_hole_gap - side_button_width / 2,
                (case_depth - side_button_height) / 2 - side_button_hole_gap
            ])
            filletBoxLeft(
                panel_border_left + case_inner_padding_left + side_button_base + side_button_extrude + thick_border_add,
                side_button_width + side_button_hole_gap * 2,
                side_button_height + side_button_hole_gap * 2,
                r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                fn=12
            );
            if (side_button_base_inner > 0) {
                translate([
                    panel_border_left + case_inner_padding_left - side_button_base_inner - 0.01,
                    side_button * frame_full_height - side_button_base_border - side_button_hole_gap - side_button_width / 2,
                    max((case_depth - side_button_height) / 2 - side_button_base_border - side_button_hole_gap, -0.01)
                ])
                cube([
                    side_button_base + side_button_base_inner + 0.02,
                    side_button_width + side_button_base_border * 2 + side_button_hole_gap * 2,
                    min(side_button_height + side_button_base_border * 2 + side_button_hole_gap * 2, case_depth) + 0.01,
                ]);
            }
        }

        // side buttons right
        for (side_button = side_buttons_right) {
            translate([
                frame_full_width - panel_border_right - case_inner_padding_right,
                side_button * frame_full_height - side_button_hole_gap - side_button_width / 2, 
                (case_depth - side_button_height) / 2 - side_button_hole_gap
            ])
            filletBoxRight(
                panel_border_right + case_inner_padding_right + side_button_base + side_button_extrude + thick_border_add,
                side_button_width + side_button_hole_gap * 2,
                side_button_height + side_button_hole_gap * 2,
                r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                fn=12
            );
            if (side_button_base_inner > 0) {
                translate([
                    frame_full_width - panel_border_right - case_inner_padding_right - side_button_base - 0.01, 
                    side_button * frame_full_height - side_button_base_border - side_button_hole_gap - side_button_width / 2,
                    max((case_depth - side_button_height) / 2 - side_button_base_border - side_button_hole_gap, -0.01)
                ])
                cube([
                    side_button_base + side_button_base_inner + 0.02,
                    side_button_width + side_button_base_border * 2 + side_button_hole_gap * 2,
                    min(side_button_height + side_button_base_border * 2 + side_button_hole_gap * 2, case_depth) + 0.01,
                ]);
            }
        }

        // side buttons top
        for (side_button = side_buttons_top) {
            translate([
                side_button * frame_full_width - side_button_hole_gap - side_button_width / 2, 
                -side_button_extrude - thick_border_add,
                (case_depth - side_button_height) / 2 - side_button_hole_gap
            ])
            filletBoxUp(
                side_button_width + side_button_hole_gap * 2,
                panel_border_top + case_inner_padding_top + side_button_base + side_button_extrude + thick_border_add,
                side_button_height + side_button_hole_gap * 2,
                r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                fn=12
            );
            if (side_button_base_inner > 0) {
                translate([
                    side_button * frame_full_width - side_button_base_border - side_button_hole_gap - side_button_width / 2, 
                    panel_border_top + case_inner_padding_top - side_button_base_inner - 0.01,
                    max((case_depth - side_button_height) / 2 - side_button_base_border - side_button_hole_gap, -0.01)
                ])
                cube([
                    side_button_width + side_button_base_border * 2 + side_button_hole_gap * 2,
                    side_button_base + side_button_base_inner,
                    min(side_button_height + side_button_base_border * 2 + side_button_hole_gap * 2, case_depth) + 0.01,
                ]);
            }
        }

        // side buttons bottom
        for (side_button = side_buttons_bottom) {
            translate([
                side_button * frame_full_width - side_button_hole_gap - side_button_width / 2, 
                frame_full_height - panel_border_bottom - case_inner_padding_bottom,
                (case_depth - side_button_height) / 2 - side_button_hole_gap
            ])
            filletBoxDown(
                side_button_width + side_button_hole_gap * 2,
                panel_border_bottom + case_inner_padding_bottom + side_button_base + side_button_extrude + thick_border_add + 0.01,
                side_button_height + side_button_hole_gap * 2,
                r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                fn=12
            );
            if (side_button_base_inner > 0) {
                translate([
                    side_button * frame_full_width - side_button_base_border - side_button_hole_gap - side_button_width / 2, 
                    frame_full_height - panel_border_bottom - case_inner_padding_bottom - side_button_base - 0.01,
                    max((case_depth - side_button_height) / 2 - side_button_base_border - side_button_hole_gap, -0.01)
                ])
                cube([
                    side_button_width + side_button_base_border * 2 + side_button_hole_gap * 2,
                    side_button_base + side_button_base_inner,
                    min(side_button_height + side_button_base_border * 2 + side_button_hole_gap * 2, case_depth) + 0.01,
                ]);
            }
        }
    }
}

module sideButtons() {
    let(
        side_button_x_offset = (side_button_width + side_button_base_border * 2 + side_button_extrude * 2 + 2),
        side_button_y_offset = (side_button_height + side_button_base_border * 2 + side_button_extrude * 2 + 2),
        side_button_base_height = min(side_button_height + side_button_base_border * 2, case_depth - 2 * side_button_hole_gap),
        thick_border_add = (thick_border ? thick_border_width : 0)
    ) {
        if (side_buttons_left) {
            for (idx = [ 0 : len(side_buttons_left) - 1 ] ) {
                translate([
                    idx * side_button_x_offset,
                    side_button_base_height / 2 - side_button_height / 2,
                    0
                ])
                filletBoxBottom(
                    side_button_width,
                    side_button_height,
                    side_button_base + side_button_extrude + panel_border_left + case_inner_padding_left - side_button_base_inner + thick_border_add,
                    r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                    fn=12
                );

                translate([
                    idx * side_button_x_offset - side_button_base_border, 
                    0,
                    0
                ])
                cube([
                    side_button_width + side_button_base_border * 2,
                    side_button_base_height,
                    side_button_base
                ]);
            }
        }

        if (side_buttons_right) {
            for (idx = [ 0 : len(side_buttons_right) - 1 ] ) {
                translate([
                    idx * side_button_x_offset,
                    side_button_y_offset + side_button_base_height / 2  - side_button_height / 2,
                    0
                ])
                filletBoxBottom(
                    side_button_width,
                    side_button_height,
                    side_button_base + side_button_extrude + panel_border_right + case_inner_padding_right - side_button_base_inner + thick_border_add,
                    r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                    fn=12
                );

                translate([
                    idx * side_button_x_offset - side_button_base_border, 
                    side_button_y_offset,
                    0
                ])
                cube([
                    side_button_width + side_button_base_border * 2,
                    side_button_base_height,
                    side_button_base
                ]);
            }
        }

        if (side_buttons_top) {
            for (idx = [ 0 : len(side_buttons_top) - 1 ] ) {
                translate([
                    idx * side_button_x_offset,
                    side_button_y_offset * 2 + side_button_base_height / 2 - side_button_height / 2,
                    0
                ])
                filletBoxBottom(
                    side_button_width,
                    side_button_height,
                    side_button_base + side_button_extrude + panel_border_top + case_inner_padding_top - side_button_base_inner + thick_border_add,
                    r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                    fn=12
                );

                translate([
                    idx * side_button_x_offset - side_button_base_border, 
                    side_button_y_offset * 2,
                    0
                ])
                cube([
                    side_button_width + side_button_base_border * 2,
                    side_button_base_height,
                    side_button_base
                ]);
            }
        }

        if (side_buttons_bottom) {
            for (idx = [ 0 : len(side_buttons_bottom) - 1 ] ) {
                translate([
                    idx * side_button_x_offset,
                    side_button_y_offset * 3 + side_button_base_height / 2 - side_button_height / 2,
                    0
                ])
                filletBoxBottom(
                    side_button_width,
                    side_button_height,
                    side_button_base + side_button_extrude + panel_border_bottom + case_inner_padding_bottom - side_button_base_inner + thick_border_add,
                    r=min(side_button_height / 2 - 0.01, side_button_fillet_radius),
                    fn=12
                );

                translate([
                    idx * side_button_x_offset - side_button_base_border, 
                    side_button_y_offset * 3,
                    0
                ])
                cube([
                    side_button_width + side_button_base_border * 2,
                    side_button_base_height,
                    side_button_base
                ]);
            }
        }
    }
}


/*****************************************************************************/
/*                              Rendering                                    */
/*****************************************************************************/

difference() {
    union() {
        // Cover panel
        if (view_mode != "only_case" && view_mode != "only_thick_border" && view_mode != "stacked_case") {
            color(panel_cover_color)
            rotate(
                view_mode == "print_vertical" 
                ? [180, 180, 180]
                : view_mode == "print_horizontal" || view_mode == "only_panel_cover"
                ? frame_full_width > frame_full_height
                ? [0, 0, 180]
                : [0, 0, 0]
                : [0, 180, 0])
            translate(
                view_mode == "only_panel_cover"
                ? [-frame_full_width/2, -frame_full_height / 2, 0]
                : view_mode == "print_vertical" 
                ? [-frame_full_width/2, -frame_full_height - print_gap, 0]
                : view_mode == "print_horizontal"
                    ? frame_full_width > frame_full_height
                    ? [-frame_full_width/2, +frame_full_height/2 + print_gap, -(case_depth + back_depth)] 
                    : [frame_full_width/2+print_gap, -frame_full_height/2, -(case_depth + back_depth)]
                : [-frame_full_width/2, -frame_full_height/2, - (panel_cover_depth + panel_depth + (view_mode == "stacked" ? 0 : debug_gap))])  // stacked
            panel_cover();
        }

        if (view_mode != "only_panel_cover") {
            // Case body
            rotate(
                view_mode == "print_vertical" 
                ? [90, 180, 180] 
                : [0, 180, 0]
            )
            translate(
                view_mode == "print_vertical" 
                ? [-frame_full_width/2, -frame_full_height * vertical_print_scale, 0] 
                : [-frame_full_width/2, -frame_full_height/2, 0]
            )
            scale([1, view_mode == "print_vertical" ? vertical_print_scale : 1, 1])
            if (view_mode == "only_thick_border") {
                color(thick_border_color)
                caseThickBorder();
            } else { 
                if (kickstand) {
                    caseWithKickstand();
                } else {
                    case();
                };
            }

            if (view_mode != "only_thick_border") {
                translate([
                    frame_full_width / 2 + print_gap, 
                    - frame_full_height / 2, 
                    view_mode == "print_vertical" ? 0 : - case_depth - back_depth
                ])
                sideButtons();
            }
        }
    }

    // cut off half
    if (cross_section_percentage > 0 && cross_section_percentage < 100) {
        translate([-frame_full_width / 2 - 0.11, -400, -100])
        cube([frame_full_width * cross_section_percentage / 100 + 0.2, frame_full_height + 500, 500]);
    }
}
